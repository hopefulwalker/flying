version = '1.0.0'

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testRuntime group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'

    compile group: 'uk.co.real-logic', name: 'sbe-tool', version: '1.6.2'
    compile group: 'org.agrona', name: 'agrona', version: '0.9.5'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compile group: 'com.hazelcast', name: 'hazelcast', version: '3.5'

//    compile group: 'org.springframework', name: 'spring-core', version: '4.3.8.RELEASE'
    compile("org.springframework:spring-core:4.3.8.RELEASE") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    runtime("org.slf4j:jcl-over-slf4j:1.7.25")


    compile group: 'org.springframework', name: 'spring-beans', version: '4.3.8.RELEASE'
    compile group: 'org.springframework', name: 'spring-context', version: '4.3.8.RELEASE'
    compile group: 'org.springframework', name: 'spring-context-support', version: '4.3.8.RELEASE'
    compile group: 'org.springframework', name: 'spring-expression', version: '4.3.8.RELEASE'

    compile project(':util')
    compile project(':framework')
    compile project(':common')
    compile project(':monitor')
    compile project(':ams')

    runtime group: 'org.apache.commons', name: 'commons-pool2', version: '2.4.2'
    runtime group: 'org.apache.commons', name: 'commons-logging', version: '1.2'
    runtime group: 'com.google.guava', name: 'guava', version: '18.0'
    runtime group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

task generateOrderMsgCodec(type: JavaExec) {
    main = 'com.flying.common.msg.codec.generator.CodecGenerator'
    args = ['com.flying.oms.msg.codec.IOrderMsgCodec']
    systemProperties = ['codec.output.dir': 'src/main/java']
    classpath sourceSets.main.runtimeClasspath
}

task generateOrderMsg(type: JavaExec) {
    main = 'uk.co.real_logic.sbe.SbeTool'
    args = ['src/main/resources/com/flying/oms/msg/oms-msg.xml']
    systemProperties = ['sbe.output.dir': 'src/main/java', 'sbe.target.language': 'Java', 'sbe.validation.stop.on.error': 'true', 'sbe.validation.xsd': 'src/main/resources/com/flying/oms/msg/sbe.xsd']
    classpath configurations.compile
}

compileJava.dependsOn('generateOrderMsg')

task clientJar(type: Jar) {
    dependsOn 'classes'
    baseName = project.name + '-client'
    from fileTree("${buildDir}/classes/main")
    include '**/client/**'
    include '**/model/**'
    include '**/msg/**'
    exclude '**/config/**'
    exclude '**/handler/**'
}

assemble.dependsOn('clientJar')